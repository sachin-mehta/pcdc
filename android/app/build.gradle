apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'jacoco'
android {
  namespace "com.meter.giga"
  compileSdk rootProject.ext.compileSdkVersion
  defaultConfig {
    applicationId "com.meter.giga"
    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion rootProject.ext.targetSdkVersion
    versionCode 18
    versionName "1.1.18"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    aaptOptions {
      // Files and dirs to omit from the packaged assets dir, modified to accommodate modern web apps.
      // Default: https://android.googlesource.com/platform/frameworks/base/+/282e181b58cf72b6ca770dc7ca5f91f135444502/tools/aapt/AaptAssets.cpp#61
      ignoreAssetsPattern '!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~'
    }
  }
  kotlinOptions {
    jvmTarget = '21'
  }
  signingConfigs {
    release {
      storeFile file(KEYSTORE_PATH)
      storePassword KEYSTORE_PASSWORD
      keyAlias KEY_ALIAS
      keyPassword KEY_PASSWORD
    }
  }
  buildTypes {
    debug {
      testCoverageEnabled true
      debuggable true
      minifyEnabled false
      applicationIdSuffix ".debug"
      versionNameSuffix
    }
    release {
      debuggable false
      minifyEnabled true
      shrinkResources true
      applicationIdSuffix ".debug" // Need to remove it once we got the crashlytics json config
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release
    }
  }


//  flavorDimensions "environment"
//
//  productFlavors {
//    dev {
//      dimension "environment"
//      buildConfigField "String", "BASE_URL", "\"${BASE_URL_DEVELOPMENT}\""
//
//    }
//    staging {
//      dimension "environment"
//      buildConfigField "String", "BASE_URL", "\"${BASE_URL_STAGING}\""
//
//    }
//    prod {
//      dimension "environment"
//      buildConfigField "String", "BASE_URL", "\"${BASE_URL_PRODUCTION}\""
//    }
//  }
  buildFeatures {
    buildConfig true
  }

  jacoco {
    version = "0.8.12"
  }
  testOptions {
    unitTests.includeAndroidResources = true
  }
}

repositories {
  flatDir {
    dirs '../capacitor-cordova-android-plugins/src/main/libs', 'libs'
  }
}

dependencies {
  implementation fileTree(include: ['*.jar'], dir: 'libs')
  implementation "com.squareup.okhttp3:okhttp:4.12.0"
  implementation "com.squareup.okhttp3:logging-interceptor:4.9.0"
  implementation 'com.squareup.retrofit2:retrofit:2.9.0'
  implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
  implementation 'androidx.lifecycle:lifecycle-service:2.9.3'
  implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.9.0'
  implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
  implementation "androidx.coordinatorlayout:coordinatorlayout:$androidxCoordinatorLayoutVersion"
  implementation "androidx.core:core-splashscreen:$coreSplashScreenVersion"
  implementation "androidx.security:security-crypto:1.1.0"
  implementation "androidx.datastore:datastore-preferences:1.1.7"
  implementation 'io.sentry:sentry-android:1.7.30'
  implementation project(':capacitor-android')
  implementation project(':ndt7lib')
  testImplementation "junit:junit:$junitVersion"
  androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
  androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoCoreVersion"
  testImplementation "org.robolectric:robolectric:4.12.1"
  // Mockito Core
  testImplementation 'org.mockito:mockito-core:5.14.1'
  testImplementation "io.mockk:mockk:1.13.10"
  testImplementation("androidx.test:core:1.7.0")
  // Mockito for Kotlin (optional but recommended)
  testImplementation 'org.mockito.kotlin:mockito-kotlin:5.2.1'
  testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3"
  testImplementation "junit:junit:4.13.2" // or your version
  testImplementation "org.jetbrains.kotlin:kotlin-reflect"
  testImplementation "com.squareup.okhttp3:okhttp:4.12.0"
  androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito:2.28.3'
  implementation project(':capacitor-cordova-android-plugins')
  implementation "org.jetbrains.kotlin:kotlin-test:2.1.21"
}

apply from: 'capacitor.build.gradle'
apply plugin: 'org.jetbrains.kotlin.android'

try {
  def servicesJSON = file('google-services.json')
  if (servicesJSON.text) {
    apply plugin: 'com.google.gms.google-services'
  }
} catch (Exception e) {
  logger.info("google-services.json not found, google-services plugin not applied. Push Notifications won't work")
}

// Use tasks.register for modern Gradle versions, which is the standard now.
tasks.register('jacocoTestReport', JacocoReport) {
  // Group and description for the task (good practice)
  group = "Reporting"
  description = "Generate Jacoco code coverage report for the debug build."

  // Specify which tests should be included in the report
  dependsOn("testDebugUnitTest")

  // --- Configuration for the report ---

  reports {
    xml.required = true   // Often needed for CI/CD tools like SonarQube, Jenkins
    html.required = true  // Generates a human-readable HTML report
  }

  // --- Defining what code to analyze ---

  // A list of all your source code directories
  def sourceDirs = [
    "src/main/java",
    "src/main/kotlin"
    // Add other source directories if you have them, e.g., "src/debug/java"
  ]
  sourceDirectories.setFrom(files(sourceDirs))

  // A list of all compiled .class files. We target the debug build.
  def classDirs = fileTree(
    dir: "${buildDir}/tmp/kotlin-classes/debug",
    excludes: [
      '**/R.class',
      '**/R$*.class',
      '**/BuildConfig.*',
      '**/Manifest*.*',
      '**/*Test*.*',
      'android/**/*.*'
    ]
  )
  classDirectories.setFrom(files([classDirs]))

  // The location of the test execution data
  executionData.setFrom(files("${buildDir}/outputs/unit_test_code_coverage/debugUnitTest/testDebugUnitTest.exec"))
}

tasks.withType(Test).configureEach {
  jacoco.includeNoLocationClasses = true
  jacoco.excludes = ['jdk.internal.*']
}
